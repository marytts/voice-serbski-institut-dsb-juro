plugins {
    id 'de.undercouch.download' version '5.3.0'
    id 'org.m2ci.msp.flaml' version '0.4.0'
    id 'de.dfki.mary.voicebuilding-legacy'
}

group 'de.dfki.mary'
version '0.1.0-beta1'

repositories {
    mavenCentral()
}

configurations.all {
    resolutionStrategy {
        force 'de.dfki.mary:marytts-lang-dsb:0.1.0'
    }
}

marytts {
    voice {
        name = 'serbski-institut-dsb-juro'
        gender = 'male'
        language = 'dsb'
        type = 'unit selection'
        samplingRate = 16000
    }
}

def downloadDataTask = tasks.register('downloadData') {
    ext.flacFile = objects.fileProperty()
            .convention(layout.buildDirectory.file('serbski-institut-dsb-data.flac'))
    ext.yamlFile = objects.fileProperty()
            .convention(layout.buildDirectory.file('serbski-institut-dsb-data.yaml'))
    outputs.file flacFile
    outputs.file yamlFile
    doLast {
        def baseUrl = 'https://github.com/marytts/serbski-institut-dsb-data/releases/download/v0.1.0'
        download.run {
            src([
                    "$baseUrl/serbski-institut-dsb-data.flac",
                    "$baseUrl/serbski-institut-dsb-data.yaml"
            ])
            dest buildDir
            overwrite false
        }
    }
}

flaml {
    flacFile = downloadDataTask.get().flacFile
    yamlFile = downloadDataTask.get().yamlFile
}

extractWavFiles {
    destDir = layout.buildDirectory.dir('wavOrig')
}

wav {
    srcDir = tasks.named('extractWavFiles').get().destDir
}

basenames {
    textDir = tasks.named('extractTextFiles').get().destDir
    labDir = tasks.named('extractLabFiles').get().destDir
    exclude 'Miks26'
}
